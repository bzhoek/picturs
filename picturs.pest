WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
// WHITESPACE = { ( " "* ~ "\\" ~ NEWLINE ~ " "* ) }
picture = _{ SOI ~ statements* ~ EOI }
statements = _{ comment | group | container | shape | font_config | unit_config | box_config | circle_config | line_config | continue_from | continuation | flow_to | move_to }
comment = @{ ("#" | "//") ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
shape = _{ dot | arrow | line | sline | rectangle | file | circle | ellipse | cylinder | oval | text | path }

flow_cmd = _{ "flow"? ~ continuation }
continuation = { "down" | "right" | "top" | "left" }

continue_from = { continue_prefix? ~ continue_direction  }
continue_prefix = _{ "continue" ~ "from"? }
continue_direction = { "right-top" | "down-left" }

// config
unit_config = { "set unit" ~ unit }
font_config = { "set font" ~ string? ~ size? }
box_config = { "set box" ~ (padding | width| height | radius | space )* }
circle_config = { "set circle" ~ (height) }
line_config = { "set line" ~ length }

// closed shapes
group = { "group" ~ identified? ~ closed_attributes ~ "{" ~ statements* ~ "}" }
container = { "box" ~ identified? ~ closed_attributes ~ "{" ~ statements* ~ "}" }
rectangle = { "box" ~ identified? ~ closed_attributes }
file = { "file" ~ identified? ~ closed_attributes }
circle = { "circle" ~ identified? ~ closed_attributes }
ellipse = { "ellipse" ~ identified? ~ closed_attributes }
cylinder = { "cylinder" ~ identified? ~ closed_attributes }
oval = { "oval" ~ identified? ~ closed_attributes }

closed_attributes = { closed_attribute* }
closed_attribute = _{ string | stroke | fill | width | height | padding | radius | space | location | endings | text_color | flow_cmd | same | thickness | effect }
stroke = { ("stroke" | "color") ~ color }
fill = { "fill" ~ id }
width = { ("wd" | "wid" | "width") ~ size_ }
height = { ("ht" | "height") ~ size_ }
padding = { ("pd" | "padding") ~ size_ }
radius = { ("rd" | "rad" | "radius") ~ size_ }
space = { ("sp" | "space" ) ~ size_ }
text_color = { "text" ~ id }

color = _{ ( id | rgb) }
rgb = ${ "#" ~ ASCII_HEX_DIGIT{6} }

// open shapes
arrow = { "arrow" ~ open_attributes }
line = { "line" ~ identified? ~ open_attributes }
sline = { "sline" ~ identified? ~ open_attributes }
path = { "path" ~ identified? ~ open_attributes }

open_attributes = { open_attribute* }
open_attribute = _{ stroke | length | source | target | abs_movement | rel_movement | caption | endings | same | thickness }

length = { ("ln" | "length") ~ size_ }
source = { ("from" | "at") ~ object_fraction }
target = { "end" ~ object_fraction }

object_fraction = _{ fraction? ~ object_edge }
fraction = ${ number ~ "/" ~ number }

endings = ${ left_end? ~ "-" ~ right_end? }
left_end = { "<" | "*" }
right_end = { ">" | "*" }

caption = { string ~ alignment? ~ opaque? }
alignment = { "left" | "right" | "above" | "below" | "nw" | "ne" | "sw" | "se" | "ljust" }
opaque = { "opaque" }

// text
text = { "text" ~ identified? ~ text_attributes }
text_attributes = { text_attribute* }
text_attribute = _{ string | stroke | fill | width | location | fit }
fit = { "fit" }

// common
identified = ${ "." ~ id }
id = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

size = { size_ }
size_ = _{ number ~ unit? }
number = @{ (ASCII_DIGIT+ ~ decimals?) | decimals }
decimals = _{ "." ~ ASCII_DIGIT+ }
unit = { "in" | "cm" | "mm" | "pt" | "pc" | "px" | "u" }

location = { edge? ~ rel_movement+ ~ (last_object | from_object)? }
rel_movement = { offset ~ direction }
offset = { size_ }
direction = { "up" | "down" | "left" | "right" }
last_object = { "last" ~ object_fraction }
from_object = { "from" ~ object_fraction }

same = { "same" }
thickness = { "invisible" | "invis" | "nostroke" | "thin" | "normal" | "thicker" | "thickest" | "thick" }
effect = { "dotted" | "dashed" | "solid" }

flow_to = { "flow" ~ size_ }
move_to = { "move" ~ movements }
movements = { movement* }
movement = _{ rel_movement | abs_movement }
abs_movement = { "to" ~ object_edge }

dot = { "dot" ~ identified? ~ dot_attributes }
dot_attributes = { dot_attribute* }
dot_attribute = _{ source | stroke | radius | same | caption }

object_edge = ${ id ~ edge? }
edge = ${ "." ~ (compass | hours | degrees) }
compass = { "ne" | "nw" | "n" | "e" | "se" | "sw" | "s" | "w" |  "c" }
hours = ${ ASCII_DIGIT{1,2} ~ ":" ~ ASCII_DIGIT{0,2} }
degrees = { ASCII_DIGIT{1,3} }

title = { string? }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}