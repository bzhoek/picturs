WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

picture = _{ SOI ~ statements* ~ EOI }
statements = _{ comment | container | shape | box_config | circle_config | flow | move_to }
comment = @{ ("#" | "//") ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
shape = _{ dot | arrow | line | rectangle | file | circle | ellipse | cylinder | oval | text }
flow = { "down" | "right" | "topright" | "topleft" }

box_config = { "set box" ~ (padding | width| height)* }
circle_config = { "set circle" ~ (height) }
container = { "box" ~ identified? ~ attributes ~ "{" ~ statements* ~ "}" }
rectangle = { "box" ~ identified? ~ attributes }
file = { "file" ~ identified? ~ attributes }
circle = { "circle" ~ identified? ~ attributes }
ellipse = { "ellipse" ~ identified? ~ attributes }
cylinder = { "cylinder" ~ identified? ~ attributes }
oval = { "oval" ~ identified? ~ attributes }
attributes = { attribute* }
attribute = { string | color | fill | width | height | padding | radius | location | text_color | flow }
text = { "text" ~ identified? ~ text_attributes }
text_attributes = { text_attribute* }
text_attribute = { string | color | fill | width | location }
color = { "color" ~ id }
fill = { "fill" ~ id }
text_color = { "text" ~ id }
identified = _{ "." ~ id }
id = @{ ASCII_ALPHANUMERIC* }

radius = { ("rd" | "rad" | "radius") ~ size }
width = { ("wd" | "wid" | "width") ~ size }
height = { ("ht" | "height") ~ size }
padding = { ("pd" | "padding") ~ size }
size = _{ length ~ unit? }

location = { edge? ~ displacement+ ~ (last_object | from_object)? }
last_object = _{ "last" ~ object_edge }
from_object = _{ "from" ~ object_edge }
displacement = { length ~ unit ~ direction }
length = { ASCII_DIGIT* }
unit = { "in" | "cm" | "mm" | "pt" | "pc" | "px" }
direction = { "up" | "down" | "left" | "right" }

move_to = { "move" ~ displacements }
displacements = { displacement* }

arrow = { "arrow" ~ source? ~ displacement? ~ target? }
line = { "line" ~ source? ~ displacement? ~ target? }
dot = { "dot" ~ object_edge ~ dot_attributes }
dot_attributes = { dot_attribute* }
dot_attribute = { color | radius }

source = { "from" ~ object_edge }
target = { "to" ~ object_edge }
object_edge = { id ~ edge? }
edge = { "." ~ ("ne" | "nw" | "n" | "e" | "se" | "sw" | "s" | "w" |  "c") }

title = { string? }
string = _{ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = { !("\"" | "\\") ~ ANY }
