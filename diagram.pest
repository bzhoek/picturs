WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

picture = _{ SOI ~ shapes* ~ EOI }
shapes = _{ container | shape }
shape = _{ move_to | dot | arrow | line | rectangle | text }

container = { "box" ~ identified? ~ attributes ~ "{" ~ shapes* ~ "}" }
rectangle = { "box" ~ identified? ~ attributes }
attributes = { attribute* }
attribute = { string | color | fill | width | height | radius | location | text_color }
text = { "text" ~ identified? ~ text_attributes }
text_attributes = { text_attribute* }
text_attribute = { string | color | fill | width | location }
color = { "color" ~ id }
fill = { "fill" ~ id }
text_color = { "text" ~ id }
identified = _{ "." ~ id }
id = @{ ASCII_ALPHANUMERIC* }

radius = { ("rd" | "rad" | "radius") ~ size }
width = { ("wd" | "wid" | "width") ~ size }
height = { ("ht" | "height") ~ size }

location = { edge ~ displacement* ~ "from" ~ object_edge }
size = _{ length ~ unit }
displacement = { length ~ unit ~ direction }
length = { ASCII_DIGIT* }
unit = { "in" | "cm" | "mm" | "pt" | "pc" | "px" }
direction = { "up" | "down" | "left" | "right" }

move_to = { "move" ~ displacements }
displacements = { displacement* }

arrow = { "arrow" ~ "from" ~ source ~ displacement? ~ "to" ~ target }
line = { "line" ~ "from" ~ source ~ displacement? ~ "to" ~ target }
dot = { "dot" ~ target ~ dot_attributes }
dot_attributes = { dot_attribute* }
dot_attribute = { color | radius }

source = { object_edge }
target = { object_edge }
object_edge = { id ~ edge }
edge = { "." ~ ("ne" | "nw" | "n" | "e" | "se" | "sw" | "s" | "w" |  "c") }

title = { string? }
string = _{ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = { !("\"" | "\\") ~ ANY }
