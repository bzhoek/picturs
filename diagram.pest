WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
// WHITESPACE = { ( " "* ~ "\\" ~ NEWLINE ~ " "* ) }
picture = _{ SOI ~ statements* ~ EOI }
statements = _{ comment | container | shape | font_config | unit_config | box_config | circle_config | line_config | flow | flow_to | move_to }
comment = @{ ("#" | "//") ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
shape = _{ dot | arrow | line | rectangle | file | circle | ellipse | cylinder | oval | text | path }
flow = { "down" | "right" | "top" | "left" }

// config
unit_config = { "set unit" ~ unit }
font_config = { "set font" ~ string }
box_config = { "set box" ~ (padding | width| height)* }
circle_config = { "set circle" ~ (height) }
line_config = { "set line" ~ length }

// closed shapes
container = { "box" ~ identified? ~ attributes ~ "{" ~ statements* ~ "}" }
rectangle = { "box" ~ identified? ~ attributes }
file = { "file" ~ identified? ~ attributes }
circle = { "circle" ~ identified? ~ attributes }
ellipse = { "ellipse" ~ identified? ~ attributes }
cylinder = { "cylinder" ~ identified? ~ attributes }
oval = { "oval" ~ identified? ~ attributes }

attributes = { attribute* }
attribute = _{ string | color | fill | width | height | padding | radius | location | text_color | flow | same }
color = { "color" ~ id }
fill = { "fill" ~ id }
width = { ("wd" | "wid" | "width") ~ size }
height = { ("ht" | "height") ~ size }
padding = { ("pd" | "padding") ~ size }
radius = { ("rd" | "rad" | "radius") ~ size }
text_color = { "text_color" ~ id }

// open shapes
arrow = { "arrow" ~ line_attributes }
line = { "line" ~ line_attributes }
path = { "path" ~ identified? ~ movements ~ caption? }

line_attributes = { line_attribute* }
line_attribute = _ { length | source | rel_movement | target | caption | arrows | same }
length = { ("ln" | "length") ~ size }
source = { "from" ~ fraction? ~ object_edge }
fraction = ${ number ~ "/" ~ number }
target = { "to" ~ fraction? ~ object_edge }
arrows = { "<->" | "<-" | "->"  }

caption = { string ~ alignment? ~ opaque? }
alignment = { "left" | "right" | "above" | "below" | "nw" | "ne" | "sw" | "se" | "ljust" }
opaque = { "opaque" }

// text
text = { "text" ~ identified? ~ text_attributes }
text_attributes = { text_attribute* }
text_attribute = _{ string | color | fill | width | location | fit }
fit = { "fit" }

// common
identified = { "." ~ id }
id = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

size = _{ number ~ unit? }
number = @{ ASCII_DIGIT* ~ ("." ~ ASCII_DIGIT+)? }
unit = { "in" | "cm" | "mm" | "pt" | "pc" | "px" }

location = { edge? ~ rel_movement+ ~ (last_object | from_object)? }
rel_movement = { offset ~ direction }
offset = { size }
direction = { "up" | "down" | "left" | "right" }
last_object = _{ "last" ~ object_edge }
from_object = _{ "from" ~ object_edge }

flow_to = { "flow" ~ size }
move_to = { "move" ~ movements }
movements = { movement* }
movement = _{ rel_movement | abs_movement }
abs_movement = { "to" ~ object_edge }

dot = { "dot" ~ at_object_edge? ~ dot_attributes ~ caption? }
at_object_edge = _{ "at" ~ object_edge }
dot_attributes = { dot_attribute* }
dot_attribute = { color | radius | same }
same = { "same" }

object_edge = ${ id ~ edge? }
edge = ${ "." ~ (compass | hours | degrees) }
compass = { "ne" | "nw" | "n" | "e" | "se" | "sw" | "s" | "w" |  "c" }
hours = ${ ASCII_DIGIT{1,2} ~ ":" }
degrees = { ASCII_DIGIT{1,3} }

title = { string? }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = { !("\"" | "\\") ~ ANY }
