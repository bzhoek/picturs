WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

picture = _{ SOI ~ shapes* ~ EOI }
shapes = _{ container | shape }
shape = _{ line | rectangle | text }
container = { "box" ~ identified? ~ attributes ~ "{" ~ shapes* ~ "}" }
rectangle = { "box" ~ identified? ~ attributes }
text = { "text" ~ identified? ~ string ~ location? }
attributes = { attribute* }
attribute = { string | color | fill | radius | location | text_color }
color = { "color" ~ id }
fill = { "fill" ~ id }
text_color = { "text" ~ id }
identified = _{ "." ~ id }
id = @{ ASCII_ALPHANUMERIC* }

// dimensions
radius = { radius_variants ~ size }
radius_variants = _{ "rd" | "rad" | "radius" }
width = { width_variants ~ size }
width_variants = _{ "wd" | "wid" | "width" }
height = { height_variants ~ size }
height_variants = _{ "ht" | "height" }

location = { anchor ~ distance* ~ "from" ~ edge }
size = _{ length ~ unit }
distance = { length ~ unit ~ direction }
length = { ASCII_DIGIT* }
unit = { "in" | "cm" | "mm" | "pt" | "pc" | "px" }
direction = { "up" | "down" | "left" | "right" }

line = { "line" ~ "from" ~ source ~ distance? ~ "to" ~ target }
source = { edge }
target = { edge }
edge = { id ~ anchor }
anchor = { "." ~ ("nw"  | "ne" | "n" | "e" | "se" | "s" | "sw" | "w" |  "c") }

title = { string? }
string = _{ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = { !("\"" | "\\") ~ ANY }
