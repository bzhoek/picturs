WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
// WHITESPACE = { ( " "* ~ "\\" ~ NEWLINE ~ " "* ) }
picture = _{ SOI ~ statements* ~ EOI }
statements = _{ comment | container | shape | font_config | unit_config | box_config | circle_config | line_config | flow | flow_to | move_to }
comment = @{ ("#" | "//") ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
shape = _{ dot | arrow | line | rectangle | file | circle | ellipse | cylinder | oval | text }
flow = { "down" | "right" | "top" | "left" }

unit_config = { "set unit" ~ unit }
font_config = { "set font" ~ string }
box_config = { "set box" ~ (padding | width| height)* }
circle_config = { "set circle" ~ (height) }
line_config = { "set line" ~ length }

container = { "box" ~ identified? ~ attributes ~ "{" ~ statements* ~ "}" }
rectangle = { "box" ~ identified? ~ attributes }
file = { "file" ~ identified? ~ attributes }
circle = { "circle" ~ identified? ~ attributes }
ellipse = { "ellipse" ~ identified? ~ attributes }
cylinder = { "cylinder" ~ identified? ~ attributes }
oval = { "oval" ~ identified? ~ attributes }
attributes = { attribute* }
attribute = { string | color | fill | width | height | padding | radius | location | text_color | flow }
text = { "text" ~ identified? ~ text_attributes }
text_attributes = { text_attribute* }
text_attribute = { string | color | fill | width | location | fit }
fit = { "fit" }
color = { "color" ~ id }
fill = { "fill" ~ id }
text_color = { "text_color" ~ id }
identified = { "." ~ id }
id = @{ ASCII_ALPHANUMERIC* }

radius = { ("rd" | "rad" | "radius") ~ size }
width = { ("wd" | "wid" | "width") ~ size }
height = { ("ht" | "height") ~ size }
length = { ("ln" | "length") ~ size }
padding = { ("pd" | "padding") ~ size }
size = _{ number ~ unit? }

location = { edge? ~ movement+ ~ (last_object | from_object)? }
last_object = _{ "last" ~ object_edge }
from_object = _{ "from" ~ object_edge }
movement = { offset ~ direction }
offset = { size }
number = @{ ASCII_DIGIT* ~ ("." ~ ASCII_DIGIT+)? }
unit = { "in" | "cm" | "mm" | "pt" | "pc" | "px" }
direction = { "up" | "down" | "left" | "right" }

flow_to = { "flow" ~ size }
move_to = { "move" ~ movements }
movements = { movement* }

arrow = { "arrow" ~ (length | line_attributes) ~ caption? }
line = { "line" ~ (length | line_attributes) ~ caption? }
line_attributes = _{ source? ~ movement? ~ target? }
caption = { string ~ alignment? }
alignment = { "left" | "right" | "above" | "below" | "nw" | "ne" | "sw" | "se" }
dot = { "dot" ~ at_object_edge? ~ dot_attributes ~ caption? }
at_object_edge = { "at" ~ object_edge }
dot_attributes = { dot_attribute* }
dot_attribute = { color | radius | same }
same = { "same" }

source = { "from" ~ object_edge }
target = { "to" ~ object_edge }
object_edge = ${ id ~ edge? }
edge = { "." ~ ("ne" | "nw" | "n" | "e" | "se" | "sw" | "s" | "w" |  "c") }

title = { string? }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = { !("\"" | "\\") ~ ANY }
